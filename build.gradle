plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'its'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.egovframe.go.kr/maven'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // * * * querydsl * * *
    implementation 'com.querydsl:querydsl-core' // querydsl
    implementation 'com.querydsl:querydsl-jpa' // querydsl

    // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
    annotationProcessor("jakarta.persistence:jakarta.persistence-api")
    annotationProcessor("jakarta.annotation:jakarta.annotation-api")

    // maria db 및 security 설정
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'com.auth0:java-jwt:3.14.0'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // jwt
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // POI
    implementation 'org.apache.poi:poi:3.17'
    implementation 'org.apache.poi:poi-ooxml:3.17'
    implementation 'org.apache.tika:tika-core:2.7.0'

    // etc
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'

    // egovframework
    implementation 'egovframework.rte:egovframework.rte.fdl.cmmn:3.9.0'
    implementation ('egovframework.rte:egovframework.rte.ptl.mvc:3.9.0') {
        exclude(module: 'spring-modules-validation')
        implementation 'egovframework.rte:spring-modules-validation:0.9'
    }
    implementation 'egovframework.rte:egovframework.rte.psl.dataaccess:3.9.0'
    implementation 'egovframework.rte:egovframework.rte.fdl.idgnr:3.9.0'
    implementation 'egovframework.rte:egovframework.rte.fdl.property:3.9.0'
}

// * * * querydsl * * *
def generated='src/main/generated'
sourceSets {
    main.java.srcDirs += [ generated ]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(generated)
}

clean.doLast {
    file(generated).deleteDir()
}

tasks.named('test') {
    useJUnitPlatform()
}
